# Математика

## Числовые типы
Двумя наиболее распространенными числовыми типами являются `Int32` и `Float64`.
Число в имени обозначает размер в битах: `Int32` - это 32-битный [целочисленный тип](https://en.wikipedia.org/wiki/Integer_(computer_science)), `Float64` - это 64-битное число с [плавающей точкой](https://en.wikipedia.org/wiki/Floating-point_arithmetic).

Все числовые типы допускают подчеркивание в любом месте значения.
Это полезно для более удобной записи больших чисел: `100000` можно записать как `100_000`. 

```crystal
p! 1, typeof(1)
p! 1.0, typeof(1.0)
p! 100_000, typeof(100_000)
p! 100_000.0, typeof(100_000.0)
```

Значения с плавающей точкой печатаются с десятичной запятой. Целочисленные значения без. 

**Справка**
Существует еще несколько числовых типов, но большинство из них предназначены только для особых случаев использования,
таких как двоичные протоколы, конкретные числовые алгоритмы и оптимизация производительности.
Вероятно, они вам не понадобятся для повседневных программ.

Смотрим [Integer literal reference](https://crystal-lang.org/reference/syntax_and_semantics/literals/integers.html) и [Float literal reference](https://crystal-lang.org/reference/syntax_and_semantics/literals/floats.html) для получения полной справки по всем примитивным числовым типам и альтернативным представлениям.

## Арифметика
**Равенство и сравнение**
Числа с одинаковым числовым значением считаются равными относительно оператора равенства `==`, независимо от их типа.

```crystal
p! 1 == 1,
  1 == 2,
  1.0 == 1,
  -2000.0 == -2000
```

```
$> crystal 02-comparison.cr
1 == 1           # => true
1 == 2           # => false
1.0 == 1         # => true
-2000.0 == -2000 # => true
```

Помимо оператора равенства существуют также операторы сравнения.
Они определяют соотношение между двумя значениями. Как и в случае с равенством, сравнение также не зависит от типа.

```crystal
p! 2 > 1,
  1 >= 1,
  1 < 2,
  1 <= 2
```

Универсальный оператор сравнения - это `<=>`, также называемый оператором космического корабля из-за его внешнего вида =)
Он сравнивает свои операнды и возвращает либо нулевое значение (оба операнда равны), либо положительное значение (первый операнд больше), либо отрицательное значение (второй операнд больше).
Он сочетает в себе поведение всех других операторов сравнения. 

```crystal
p! 1 <=> 1,
  2 <=> 1,
  1 <=> 2
```

```
$> crystal 04-universal_comparison.cr
1 <=> 1 # => 0
2 <=> 1 # => 1
1 <=> 2 # => -1
```

## Операторы
Со этими операторами можно выполнять основные арифметические операции.
Значение выражения - это результат операции.

```crystal
p! 1 + 1, # сложение
  1 - 1,  # вычитание
  2 * 3,  # умножение
  2 ** 4, # возведение в степень
  2 / 3,  # деление
  2 // 3, # деление без остатка
  3 % 2,  # деление по модулю (остаток от деления)
  -1      # отрицание (унарный)
```

Результат большинства этих операций между целочисленными операндами также является целочисленным значением.
Оператор деления `/` является исключением. Он всегда возвращает значение с плавающей запятой.
Оператор деления без остатка `//` возвращает целочисленное значение.
Операция между целочисленными операндами и операндами с плавающей запятой всегда возвращает значение с плавающей запятой.
В противном случае типом возвращаемого значения обычно является тип первого операнда.

**Справка**
Полный список операторов доступен в [справочнике по операторам](https://crystal-lang.org/reference/syntax_and_semantics/operators.html#arithmetic-operators).

## Числовые методы
Некоторые менее распространенные математические операции - это не операторы, а именованные методы.

```crystal
p! -5.abs,   # абсолютное значение
  4.3.round, # округление до блищайшего целого
  5.even?,   # проверка чётное/нечётное
  10.gcd(16) # наибольший общий делитель
```

**Справка**
Полный список численных методов доступен в документации [Number API](https://crystal-lang.org/api/latest/Number.html).

## Математические методы
Некоторые арифметические методы определены не непосредственно для числовых типов, а в пространстве имен `Math`.

```crystal
p! Math.cos(1),     # косинус
  Math.sin(1),      # синус
  Math.tan(1),      # тангенс
  Math.log(42),     # натуральный логарифм 
  Math.log10(312),  # логарифм на базе 10
  Math.log(312, 5), # логарифм на базе 5
  Math.sqrt(9)      # квадратный корень
```

**Справка**
Полный список математических методов доступен в документации [Math API](https://crystal-lang.org/api/latest/Math.html). 

## Константы
Некоторые математические константы доступны как константы модуля `Math`.

```crystal
p! Math::E,  # Число Эйлера
  Math::TAU, # (2 * PI)
  Math::PI   # Постоянная архимеда (TAU / 2)
```

Read on [Crystal](https://crystal-lang.org/reference/tutorials/basics/30_math.html)
